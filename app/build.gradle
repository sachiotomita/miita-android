apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        applicationId "com.naoto.yamaguchi.miita"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            def configFile = file("../deploy/miita/android/release.properties")
            def props = new Properties()
            props.load(new FileInputStream(configFile))

            storeFile file(props.storeFile)
            storePassword props.storePassword
            keyAlias props.keyAlias
            keyPassword props.keyPassword
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
            minifyEnabled false

            buildConfigField("String", "CLIENT_ID", "\"${qiita_client_id_debug}\"")
            buildConfigField("String", "CLIENT_SECRET", "\"${qiita_client_secret_debug}\"")

            ext.enableCrashlytics = false

            manifestPlaceholders = [
                    fabricApiKey: "UNDEFINED",
                    callbackHost: "callback.debug"
            ]
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            def configFile = file("../deploy/miita/android/release.properties")
            def props = new Properties()
            props.load(new FileInputStream(configFile))
            buildConfigField("String", "CLIENT_ID", "\"${props.qiitaClientID}\"")
            buildConfigField("String", "CLIENT_SECRET", "\"${props.qiitaClientSecret}\"")

            manifestPlaceholders = [
                    fabricApiKey: props.fabricApiKey,
                    callbackHost: "callback"
            ]
        }
    }
    sourceSets {
        main {
            resources.srcDirs = ['src/main/resources', 'src/androidTest/resources']
        }
        debug {
            manifest.srcFile 'src/debug/AndroidManifest.xml'
        }
        release {
            manifest.srcFile 'src/release/AndroidManifest.xml'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:support-v4:24.0.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile 'com.google.firebase:firebase-core:10.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.5.0'
    androidTestCompile('com.android.support.test:runner:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4.1') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
